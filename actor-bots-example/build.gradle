buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:3.1.0'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'nebula.deb'

group 'im.actor'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = "im.actor.bots.MainBotFarmKt"

dependencies {

    compile project(':actor-bots')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task actorBotsDeb(type: Deb) {
    release '1'

    into "/usr/lib/actor-bots"

    user 'actor'

    dependsOn installApp

    preInstall file('scripts/deb/preInstall.sh')

    from(jar.outputs.files) {
        into '/usr/lib/actor-bots'
    }
    from(configurations.runtime) {
        into '/usr/lib/actor-bots'
    }
    from('scripts/deb/bin') {
        into '/usr/bin'
        fileMode 0555
    }
    from('src/main/resources') {
        into '/usr/lib/actor-bots'
    }
    from('home') {
        createDirectoryEntry = true
        fileMode 0500
        into 'home'
    }
}